Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLINNATE_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLINNATE_InitSettings()
THEN
DB_LLINNATE_Skills_Race("REALLY_DWARF", "Target_PetrifyingTouch");
DB_LLINNATE_Skills_Race("REALLY_ELF", "Shout_FleshSacrifice");
DB_LLINNATE_Skills_Race("REALLY_HUMAN", "Shout_InspireStart");
DB_LLINNATE_Skills_Race("REALLY_LIZARD", "Cone_Flamebreath");

DB_LLINNATE_Skills_Origins("IFAN", "Summon_SoulWolf");
DB_LLINNATE_Skills_Origins("RED_PRINCE", "Target_DemonicStare");
DB_LLINNATE_Skills_Origins("SEBILLE", "Shout_BreakTheShackles");
DB_LLINNATE_Skills_Origins("BEAST", "Target_Squall");
DB_LLINNATE_Skills_Origins("LOHSE", "Target_MaddeningSong");
DB_LLINNATE_Skills_Origins("FANE", "Target_TimeWarp");
DB_LLINNATE_Skills_Origins("GENERIC", "Dome_CircleOfProtection");

//Flag, Racial Skill Replacer, Origin Skill Replacer
DB_LLINNATE_Skills_Replacers("LLINNATE_SkillReplacer_Necromancer", "Shout_LLINNATE_FleshSacrifice_Necromancer", "Storm_LLINNATE_DeathStorm");
DB_LLINNATE_Skills_Replacers("LLINNATE_SkillReplacer_TimeWarp", "", "Target_TimeWarp");
DB_LLINNATE_Skills_Replacers("LLINNATE_SkillReplacer_Elf", "Shout_FleshSacrifice", "");
DB_LLINNATE_Skills_Replacers("LLINNATE_SkillReplacer_Dwarf", "Target_PetrifyingTouch", "");
DB_LLINNATE_Skills_Replacers("LLINNATE_SkillReplacer_Human", "Shout_InspireStart", "");
DB_LLINNATE_Skills_Replacers("LLINNATE_SkillReplacer_Lizard", "Cone_Flamebreath", "");

PROC
LLINNATE_InitSettings()
THEN
DB_LLINNATE_InnateTalents("QuickStep");
DB_LLINNATE_InnateTalents("ViolentMagic");
DB_LLINNATE_InnateTalents("AnimalEmpathy");
//END_REGION

//REGION REFUND
PROC
LLINNATE_RefundPoints((CHARACTERGUID)_Player)
AND
CharacterHasTalent(_Player, "QuickStep", _Refund1)
AND
CharacterHasTalent(_Player, "ViolentMagic", _Refund2)
AND
CharacterHasTalent(_Player, "AnimalEmpathy", _Refund3)
AND
IntegerSum(_Refund1, _Refund2, _RefundCombo1)
AND
IntegerSum(_RefundCombo1, _Refund3, _RefundTalentPoints)
AND
_RefundTalentPoints > 0
THEN
CharacterDisplayTextWithParam(_Player, "LLINNATE_DisplayText_RefundedPoints", _RefundTalentPoints);
CharacterAddTalentPoint(_Player, _RefundTalentPoints);
ObjectSetFlag(_Player, "LLINNATE_RefundedTalentPoints", 0);
//END_REGION

//REGION ADD_TALENT
/* [OSITOOLS_ONLY]
PROC
LLINNATE_AddTalent((CHARACTERGUID)_Player, (STRING)_Talent)
AND
_Talent != "AnimalEmpathy"
THEN
NRD_PlayerSetBaseTalent(_Player, _Talent, 1);
CharacterAddCivilAbilityPoint(_Player, 0);

PROC
LLINNATE_AddTalent((CHARACTERGUID)_Player, "AnimalEmpathy")
AND
NRD_IsModLoaded(Kalavinkas_Combat_Enhanced_e844229e-b744-4294-9102-a7362a926f71, 0)
THEN
CharacterAddTalent(_Player, "AnimalEmpathy");

// PET_PAL_COMPATIBILITY for
// Kalavinkas_Combat_Enhanced_e844229e-b744-4294-9102-a7362a926f71
// Divinity Unleashed
PROC
LLINNATE_AddTalent((CHARACTERGUID)_Player, "AnimalEmpathy")
AND
NRD_IsModLoaded(Kalavinkas_Combat_Enhanced_e844229e-b744-4294-9102-a7362a926f71, 1)
THEN
SetTag(_Player, "LLINNATE_PETPAL");
*/

// [BEGIN_NO_OSITOOLS]
PROC
LLINNATE_AddTalent((CHARACTERGUID)_Player, (STRING)_Talent)
AND
_Talent != "AnimalEmpathy"
THEN
CharacterAddTalent(_Player, _Talent);

PROC
LLINNATE_AddTalent((CHARACTERGUID)_Player, "AnimalEmpathy")
AND
NOT SysStatus("Kalavinka_DivinityUnleashed", _)
THEN
CharacterAddTalent(_Player, "AnimalEmpathy");

PROC
LLINNATE_AddTalent((CHARACTERGUID)_Player, "AnimalEmpathy")
AND
SysStatus("Kalavinka_DivinityUnleashed", _)
THEN
SetTag(_Player, "LLINNATE_PETPAL");
// [END_NO_OSITOOLS]
//END_REGION

//REGION PET_PAL_TAG
IF
ObjectWasTagged(_Char, "LLINNATE_PETPAL")
THEN
SetTag(_Char, "PETPAL");

IF
ObjectLostTag((CHARACTERGUID)_Char, "LLINNATE_PETPAL")
AND
CharacterHasTalent(_Char, "AnimalEmpathy", 0)
THEN
ClearTag(_Char, "PETPAL");

IF
ObjectLostTag((CHARACTERGUID)_Char, "PETPAL")
AND
IsTagged(_Char, "LLINNATE_PETPAL", 1)
AND
NOT DB_LLINNATE_PreventTagSpam(_Char, "PETPAL") // In case some mod really doesn't want us to have the tag
THEN
DB_LLINNATE_PreventTagSpam(_Char, "PETPAL");
SetTag(_Char, "PETPAL");
ProcObjectTimer(_Char, "Timers_LLINNATE_ClearPetPalFailsafe", 1000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLINNATE_ClearPetPalFailsafe")
AND
DB_LLINNATE_PreventTagSpam(_Char, "PETPAL")
THEN
NOT DB_LLINNATE_PreventTagSpam(_Char, "PETPAL");
//END_REGION

//REGION ADD_TALENTS_NEW

PROC
LLINNATE_AddAllTalents((CHARACTERGUID)_Char, 1) // IsPlayer
AND
DB_LLINNATE_InnateTalents(_Talent)
AND
CharacterHasTalent(_Char, _Talent, 1)
THEN
LLINNATE_AddTalent(_Char, _Talent);

// [BEGIN_NO_OSITOOLS]
PROC
LLINNATE_AddAllTalents((CHARACTERGUID)_Char, 1) // IsPlayer
AND
DB_LLINNATE_InnateTalents(_Talent)
AND
CharacterHasTalent(_Char, _Talent, 0)
THEN
LLINNATE_AddTalent(_Char, _Talent);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LLINNATE_AddAllTalents((CHARACTERGUID)_Char, 1) // IsPlayer
AND
DB_LLINNATE_InnateTalents(_Talent)
AND
CharacterHasTalent(_Char, _Talent, 0)
THEN
LLINNATE_AddTalent(_Char, _Talent);

PROC
LLINNATE_AddAllTalents((CHARACTERGUID)_Char, 0) // NPC
AND
DB_LLINNATE_InnateTalents(_Talent)
AND
CharacterHasTalent(_Char, _Talent, 0)
THEN
NRD_ModCall("LaughingLeader_InnateTalents", "AddTalent", (STRING)_Char, _Talent);
*/
//END_REGION

//REGION CHECK_TALENTS_EVENTS
/* [OSITOOLS_ONLY]
QRY
LLINNATE_QRY_CanRefundPoints()
AND
NRD_IsModLoaded(e844229e-b744-4294-9102-a7362a926f71, 0) // Divinity Unleashed
AND
NRD_IsModLoaded(723ad06b-0241-4a2e-a9f3-4d2b419e0fe3, 0) // Divinity Conflux
THEN
DB_NOOP(1);
*/

// [BEGIN_NO_OSITOOLS]
QRY
LLINNATE_QRY_CanRefundPoints()
AND
NOT SysStatus("KCE_CoreRules_Story", _)
AND
NOT SysStatus("Enemy_Xorn_Blade_Dervish", _)
THEN
DB_NOOP(1);
// [END_NO_OSITOOLS]

IF
CharacterJoinedParty(_Origin)
AND
DB_Origins(_Origin)
AND
NOT CharacterGetHostCharacter(_Origin)
AND
CharacterIsControlled(_Origin, 0)
AND
ObjectGetFlag(_Origin, "LLINNATE_RefundedTalentPoints", 0)
AND
LLINNATE_QRY_CanRefundPoints()
THEN
LLINNATE_RefundPoints(_Origin);
ProcObjectTimerCancel(_Origin, "Timers_LLINNATE_CheckAddTalent");
ProcObjectTimer(_Origin, "Timers_LLINNATE_CheckAddTalent", 500);

PROC
Proc_ApplyPreset((CHARACTERGUID)_Char,(STRING)_Preset)
THEN
ProcObjectTimerCancel(_Char, "Timers_LLINNATE_CheckAddTalent");
ProcObjectTimer(_Char, "Timers_LLINNATE_CheckAddTalent", 500);

IF
StoryEvent((CHARACTERGUID)_Char, "LeaderLib_PresetApplied")
THEN
ProcObjectTimerCancel(_Char, "Timers_LLINNATE_CheckAddTalent");
ProcObjectTimer(_Char, "Timers_LLINNATE_CheckAddTalent", 500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLINNATE_CheckAddTalent")
AND
CharacterIsPlayer(_Char, _IsPlayer)
THEN
LLINNATE_AddAllTalents(_Char, _IsPlayer);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char, "Timers_LLINNATE_CheckAddTalent")
AND
CharacterHasTalent(_Char, "QuickStep", 1)
AND
CharacterHasTalent(_Char, "ViolentMagic", 1)
AND
CharacterHasTalent(_Char, "AnimalEmpathy", 1)
AND
ObjectGetFlag(_Char, "LLINNATE_AddTalents_FailsafeCheck", 1)
THEN
ObjectClearFlag(_Char, "LLINNATE_AddTalents_FailsafeCheck", 0);
//END_REGION

//REGION RE_INITIALIZE
IF
GlobalFlagSet("LLINNATE_RestartInitialization")
THEN
GlobalClearFlag("LLINNATE_RestartInitialization");

IF
GlobalFlagSet("LLINNATE_RestartInitialization")
AND
DB_IsPlayer(_Player)
THEN
ProcObjectTimerCancel(_Player, "Timers_LLINNATE_CheckAddTalent");
ProcObjectTimer(_Player, "Timers_LLINNATE_CheckAddTalent", 500);
//END_REGION

//REGION SKILL_REPLACERS
IF
ObjectFlagSet(_ReplacerFlag, (CHARACTERGUID)_Character, _Instance)
AND
DB_LLINNATE_Skills_Replacers(_ReplacerFlag, _RacialSkillResult, _OriginSkillResult)
THEN
ObjectClearFlag(_Character, _ReplacerFlag);
LLINNATE_ReplaceRacialSkill(_Character, _RacialSkillResult);
LLINNATE_ReplaceOriginSkill(_Character, _OriginSkillResult);
ObjectSetFlag(_Character, "LLINNATE_CanReverseSkillReplacer");

PROC
LLINNATE_RemoveReplacedRacialSkill((CHARACTERGUID)_Character)
AND
GetVarString(_Character, "LLINNATE_NewRacialSkill", _Skill)
AND
_Skill != ""
THEN
CharacterRemoveSkill(_Character, _Skill);
SetVarString(_Character, "LLINNATE_NewRacialSkill", "");

PROC
LLINNATE_ReplaceRacialSkill((CHARACTERGUID)_Character, (STRING)_NewSkill)
AND
_NewSkill != ""
AND
DB_LLINNATE_Skills_Race(_Tag, _Skill)
AND
_Skill != _NewSkill
AND
IsTagged(_Character, _Tag, 1)

THEN
CharacterRemoveSkill(_Character, _Skill);

PROC
LLINNATE_ReplaceRacialSkill((CHARACTERGUID)_Character, (STRING)_NewSkill)
AND
_NewSkill != ""
THEN
LLINNATE_RemoveReplacedRacialSkill(_Character);
CharacterAddSkill(_Character, _NewSkill, 1);
SetVarString(_Character, "LLINNATE_NewRacialSkill", _NewSkill);

PROC
LLINNATE_RemoveReplacedOriginSkill((CHARACTERGUID)_Character)
AND
GetVarString(_Character, "LLINNATE_NewOriginSkill", _Skill)
AND
_Skill != ""
THEN
CharacterRemoveSkill(_Character, _Skill);
SetVarString(_Character, "LLINNATE_NewOriginSkill", "");

PROC
LLINNATE_ReplaceOriginSkill((CHARACTERGUID)_Character, (STRING)_NewSkill)
AND
_NewSkill != ""
AND
DB_LLINNATE_Skills_Origins(_Tag, _Skill)
AND
IsTagged(_Character, _Tag, 1)
THEN
CharacterRemoveSkill(_Character, _Skill);

PROC
LLINNATE_ReplaceOriginSkill((CHARACTERGUID)_Character, (STRING)_NewSkill)
AND
_NewSkill != ""
THEN
LLINNATE_RemoveReplacedOriginSkill(_Character);
CharacterAddSkill(_Character, _NewSkill, 1);
SetVarString(_Character, "LLINNATE_NewOriginSkill", _NewSkill);

IF
ObjectFlagSet("LLINNATE_Commands_ReverseSkillReplacer", (CHARACTERGUID)_Character, _Instance)
THEN
ObjectClearFlag(_Character, "LLINNATE_Commands_ReverseSkillReplacer");
LLINNATE_RemoveReplacedRacialSkill(_Character);
LLINNATE_RemoveReplacedOriginSkill(_Character);
LLINNATE_RevertReplacedSkills(_Character);

PROC
LLINNATE_RevertReplacedSkills((CHARACTERGUID)_Character)
AND
DB_LLINNATE_Skills_Race(_Tag, _Skill)
AND
IsTagged(_Character, _Tag, 1)
THEN
CharacterAddSkill(_Character, _Skill, 1);

PROC
LLINNATE_RevertReplacedSkills((CHARACTERGUID)_Character)
AND
DB_LLINNATE_Skills_Origins(_Tag, _Skill)
AND
IsTagged(_Character, _Tag, 1)
THEN
CharacterAddSkill(_Character, _Skill, 1);

IF
TextEventSet("llinnate_settings")
AND
CharacterGetHostCharacter(_Host)
AND
QRY_SpeakerIsAvailable(_Host)
THEN
Proc_StartDialog(0, "LLINNATE_SettingsMenu", _Host, _Host);
//END_REGION

//REGION VERSIONING
// [BEGIN_NO_OSITOOLS]
IF
GameStarted(_Level, _)
AND
IsGameLevel(_Level, 1)
AND
LLINNATE_Updater_QRY_UpdateNeeded("1.2.0.0")
THEN
LLINNATE_Internal_RegisterMod();

PROC
LLINNATE_Internal_RegisterMod()
THEN
LLINNATE_Updater_RemoveOldVersions("1.2.0.0");
LLINNATE_Updater_SetVersion("1.2.0.0");

QRY
LLINNATE_Updater_QRY_UpdateNeeded((STRING)_Version)
AND
NOT DB_Mods_Registered("InnateTalents", "LaughingLeader", _Version)
THEN
DB_NOOP(1);

PROC
LLINNATE_Updater_RemoveOldVersions((STRING)_NextVersion)
AND
DB_Mods_Registered("InnateTalents", "LaughingLeader", _Version)
THEN
NOT DB_Mods_Registered("InnateTalents", "LaughingLeader", _Version);
LLINNATE_Updater_VersionUpdated(_Version, _NextVersion);

PROC
LLINNATE_Updater_VersionUpdated((STRING)_Version, (STRING)_NextVersion)
THEN
DB_NOOP(1);

PROC
LLINNATE_Updater_SetVersion((STRING)_Version)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
//Fire LeaderLib ModUpdated procs.
DB_LeaderLib_ModApi_RegisterMod("InnateTalents", "LaughingLeader", _Version);

PROC
LLINNATE_Updater_SetVersion((STRING)_Version)
AND
NOT GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
DB_Mods_Registered("InnateTalents", "LaughingLeader", _Version);
// [END_NO_OSITOOLS]
//END_REGION

//REGION LEADERLIB
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_LLINNATE_RegisteredLeaderLibSettings(_)
THEN
DB_LLINNATE_RegisteredLeaderLibSettings(1);

IF
DB_LLINNATE_RegisteredLeaderLibSettings(1)
THEN
DB_LeaderLib_ModApi_RegisterMenu("LaughingLeader.InnateTalents", "[Innate Talents] Settings", "LLINNATE_SettingsMenu", "InnateTalents", "LaughingLeader");
DB_LeaderLib_ModApi_RegisterActiveGoal("InnateTalents", "LaughingLeader", "LaughingLeader_InnateTalents");
//END_REGION

//REGION LOST_WALLET
IF
GameStarted(_Level, _)
AND
IsGameLevel(_Level, 1)
AND
NOT DB_GlobalFlag("LLINNATE_SpawnedLostWallet")
AND
CharacterGetHostCharacter(_Host)
AND
GetPosition(_Host, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LLRACE_LostWallet_d3215b98-ddcc-41bd-ad46-e41b14216014", _x, _y, _z, _Wallet)
AND
FindValidPosition(_x, _y, _z, 10.0, _Wallet, _tx, _ty, _tz)
THEN
GlobalSetFlag("LLINNATE_SpawnedLostWallet");
TeleportToPosition(_Wallet, _tx, _ty, _tz, "", 0);

IF
CharacterUsedItemTemplate(_Player, "LLRACE_LostWallet_d3215b98-ddcc-41bd-ad46-e41b14216014", _Wallet)
AND
DB_IsPlayer(_Player)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QRY_SpeakerIsAvailable(_Wallet)
THEN
Proc_StartDialog(0, "LLINNATE_LostWallet", _Wallet, _Player);

IF
DialogEnded("LLINNATE_LostWallet", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (ITEMGUID)_Wallet)
AND
ObjectGetFlag(_Wallet, "LLINNATE_LostWallet_TookGold", 1)
THEN
ItemDestroy(_Wallet);

IF
ObjectFlagSet("LLINNATE_LostWallet_AllGold", (CHARACTERGUID)_Player, _)
THEN
CharacterAddGold(_Player, 2000);

IF
ObjectFlagSet("LLINNATE_LostWallet_HalfGold", (CHARACTERGUID)_Player, _)
THEN
CharacterAddGold(_Player, 1000);

IF
ObjectFlagSet("LLINNATE_LostWallet_SomeGold", (CHARACTERGUID)_Player, _)
THEN
CharacterAddGold(_Player, 250);
CharacterStatusText(_Player, "<font color='#97FBFF'>Saintly</font>");
PlayEffect(_Player, "RS3_FX_Skills_Divine_Bless_Impact_01");
//END_REGION

//REGION COMMANDS
IF
ObjectFlagSet("LLINNATE_Commands_AddResurrection", (CHARACTERGUID)_Player, _)
THEN
CharacterAddSkill(_Player, "Teleportation_StoryModeFreeResurrect", 1);
ObjectClearFlag(_Player, "LLINNATE_Commands_AddResurrection", 0);

IF
ObjectFlagSet("LLINNATE_Commands_RemoveResurrection", (CHARACTERGUID)_Player, _)
THEN
CharacterRemoveSkill(_Player, "Teleportation_StoryModeFreeResurrect");
ObjectClearFlag(_Player, "LLINNATE_Commands_RemoveResurrection", 0);
//END_REGION

//REGION LUA_CURRENT_LEVEL
/* [OSITOOLS_ONLY]
IF
GameStarted(_Region, _)
THEN
NRD_ModCall("LaughingLeader_InnateTalents", "SendRegionToClients", _Region);

IF
UserConnected(_User, _, _)
AND
DB_CurrentLevel(_Region)
THEN
NRD_ModCall("LaughingLeader_InnateTalents", "SendRegionToClients", _Region);
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
